import groovy.json.JsonSlurper

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

def configFile = file('../config.json')
def config = new JsonSlurper().parseText(configFile.text)

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId = config.applicationId
        minSdkVersion 25
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [
                applicationName: config.applicationName
        ]
        buildConfigField "String", "API_URL", "\"http://${config.backend.host}:${config.backend.port}/${config.backend.endpoint}\""
    }
    signingConfigs {
        release {
            storeFile file(config.signingConfig.storeFile)
            storePassword config.signingConfig.storePassword
            keyAlias config.signingConfig.keyAlias
            keyPassword config.signingConfig.keyPassword
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
        }
    }
    dataBinding {
        enabled = true
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'

    // Moshi - Json converter
    def moshi_version = "1.8.0"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    annotationProcessor "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    // Kotlin Coroutines - create не блокирующих запросов
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3"

    // Retrofit2
    def retrofit_version = "2.6.2"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2"

    // OkHTTP3
    def okhttp3_version = "4.2.2"
    implementation "com.squareup.okhttp3:okhttp:$okhttp3_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3_version"

    // Room
    def room_version = "2.2.4"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

}
